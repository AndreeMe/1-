#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("НастройкаСклада") Тогда
		НастройкаСклада = Параметры.Отбор.НастройкаСклада;	
		Использование = Истина;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "НастройкаСклада", НастройкаСклада, ВидСравненияКомпоновкиДанных.Равно,,Использование);
	КонецЕсли;
	
	ОтображениеСкрытыхПользователей = Истина;               
	ОтобразитьСкрытыхПользователейНажатиеНаСервере(); 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтобразитьСкрытыхПользователейНажатие(Элемент)
	ОтобразитьСкрытыхПользователейНажатиеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВАСХ(Команда) 
	
	ОтправитьВАСХНаСервере();  
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	ОбновитьСписокНаСервере();
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПользователя(Команда)
	УдалитьПользователейНаСервере();
	Элементы.Список.Обновить();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьСписокНаСервере()     
	
	Если Список.Отбор.Элементы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаСклада = Список.Отбор.Элементы[0].ПравоеЗначение;

	Если НЕ ТипЗнч(НастройкаСклада) = Тип("СправочникСсылка.дкл_НастройкиСкладовЛифтов") Тогда
		Возврат;	
	КонецЕсли;	
	
	Результат = дкл_ОбменДаннымиСервер.ПолучитьСписокПользователей(НастройкаСклада); 
	
	Если НЕ ТипЗнч(Результат) = Тип("Структура") ИЛИ НЕ Результат.Свойство("user") Тогда
		Возврат;	
	КонецЕсли;
	
	Для каждого Стр Из Результат.user Цикл
		
		НаборЗаписей = РегистрыСведений.дкл_ПользователиСкладов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.НастройкаСклада.Установить(НастройкаСклада);
		НаборЗаписей.Отбор.userId.Установить(Стр.userId);
		НаборЗаписей.Прочитать();
		Если НЕ НаборЗаписей.Количество() = 0 Тогда
			ТекущаяЗапись = НаборЗаписей[0];
		Иначе
			ТекущаяЗапись = НаборЗаписей.Добавить();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ТекущаяЗапись, Стр, ,"password, remotePassword");
		ТекущаяЗапись.НастройкаСклада = НастройкаСклада;
		ТекущаяЗапись.active_ = Стр.active;                  
		Если Стр.role = "Оператор" ИЛИ ВРег(Стр.role) = "OPERATOR" Тогда
			ТекущаяЗапись.Роль = Перечисления.дкл_РолиПользователей.Оператор;
		ИначеЕсли Стр.role = "Администратор" ИЛИ ВРег(Стр.role) = "ADMIN" Тогда
			ТекущаяЗапись.Роль = Перечисления.дкл_РолиПользователей.Администратор;
		КонецЕсли;               
		НаборЗаписей.ДополнительныеСвойства.Вставить("НеДополнять", Истина);
		НаборЗаписей.Записать();
	
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОтправитьВАСХНаСервере()
	
	Если Список.Отбор.Элементы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаСклада = Список.Отбор.Элементы[0].ПравоеЗначение;
	
	МассивUserId = Новый Массив;
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	Для Каждого Стр Из ВыделенныеСтроки Цикл
		МассивUserId.Добавить(Стр.userId);		
	КонецЦикла;

	дкл_ОбменДаннымиСервер.ВыгрузитьПользователейСклада(
			НастройкаСклада, ,МассивUserId);	
		
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСкрытыхПользователейНажатиеНаСервере() 
	Если ОтображениеСкрытыхПользователей Тогда
		Элементы.ОтобразитьСкрытыхПользователей.Заголовок = "Отобразить неактивных пользователей";
		Использование = Истина;
		ОтображениеСкрытыхПользователей = Ложь;
	Иначе
		Элементы.ОтобразитьСкрытыхПользователей.Заголовок = "Скрыть неактивных пользователей";
		Использование = Ложь;
		ОтображениеСкрытыхПользователей = Истина;
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
	Список, "active_", Истина, ВидСравненияКомпоновкиДанных.Равно,,Использование);
КонецПроцедуры

&НаСервере
Процедура УдалитьПользователейНаСервере()

	НастройкаСклада = Список.Отбор.Элементы[0].ПравоеЗначение;
	Ответ = дкл_ОбменДаннымиСервер.ПолучитьСписокПользователей(НастройкаСклада); 

	МассивUserId = Новый Массив;
	Для Каждого user Из Ответ.user Цикл
		МассивUserId.Добавить(user.userId)
	КонецЦикла;

	МассивUserIdКУдалению = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из Элементы.Список.ВыделенныеСтроки Цикл
		Если МассивUserId.Найти(ВыделеннаяСтрока.userId) = Неопределено Тогда
			МассивUserIdКУдалению.Добавить(ВыделеннаяСтрока.userId);		
		КонецЕсли;		
	КонецЦикла;
	
	Если НЕ МассивUserIdКУдалению.Количество() = 0 Тогда
		РегистрыСведений.дкл_ПользователиСкладов.УдалитьСтроки(НастройкаСклада, МассивUserIdКУдалению);	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти





