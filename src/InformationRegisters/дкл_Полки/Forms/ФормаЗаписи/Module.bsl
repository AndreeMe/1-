#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//@skip-check using-isinrole
	Если НЕ РольДоступна(Метаданные.Роли.дкл_Администратор) 
	  И НЕ РольДоступна(Метаданные.Роли.ПолныеПрава) Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	РеквизитыПолки = Новый Структура("trayNumber, title, barcode");
	ЗаполнитьЗначенияСвойств(РеквизитыПолки, ТекущийОбъект);
	
	Попытка
		Ответ = дкл_ОбменДаннымиСервер.ОбновитьПолкуВЛифте(ТекущийОбъект.НастройкаСклада, РеквизитыПолки);
		Если ЗначениеЗаполнено(Ответ.errorCode) Тогда               
			Текст = НСтр("ru = '%1 (код ошибки %2)'");
			Текст = СтрШаблон(Текст, Ответ.message, Ответ.errorCode);
			ОбщегоНазначения.СообщитьПользователю(Текст,,,,Отказ);
		КонецЕсли;
	Исключение
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()),,,,Отказ);
	КонецПопытки;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// отдельно изменение возможности редактирования полки

	РеквизитыПолки = Новый Структура("trayNumber, Editable");
	ЗаполнитьЗначенияСвойств(РеквизитыПолки, ТекущийОбъект);

	Попытка
		Ответ = дкл_ОбменДаннымиСервер.РедактированиеПолкиВЛифте(ТекущийОбъект.НастройкаСклада, РеквизитыПолки);
		// ОбщегоНазначения.СообщитьПользователю(Ответ.message);
	Исключение
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()),,,,Отказ);
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЯрусНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	ДанныеВыбора.ЗагрузитьЗначения(ЯрусыСклада(Запись.НастройкаСклада));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ЯрусыСклада(НастройкаСклада)
	
	ЗначенияРеквизитовОбъекта = 
		ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаСклада, "Стеллаж, Линия, Склад");

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Склад"  , ЗначенияРеквизитовОбъекта.Склад);
	Запрос.УстановитьПараметр("Стеллаж", ЗначенияРеквизитовОбъекта.Стеллаж);
	Запрос.УстановитьПараметр("СтеллажНеУстановлен", НЕ ЗначениеЗаполнено(ЗначенияРеквизитовОбъекта.Стеллаж));
	Запрос.УстановитьПараметр("Линия"  , ЗначенияРеквизитовОбъекта.Линия);              
	Запрос.УстановитьПараметр("ЛинияНеУстановлена" , НЕ ЗначениеЗаполнено(ЗначенияРеквизитовОбъекта.Линия));
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СкладскиеЯчейки.Ярус КАК Ярус
	|ИЗ
	|	Справочник.СкладскиеЯчейки КАК СкладскиеЯчейки
	|ГДЕ
	|	НЕ СкладскиеЯчейки.Ярус = """"     
	|	И СкладскиеЯчейки.Владелец = &Склад
	|	И (&ЛинияНеУстановлена  ИЛИ СкладскиеЯчейки.Линия   = &Линия)
	|	И (&СтеллажНеУстановлен ИЛИ СкладскиеЯчейки.Стеллаж = &Стеллаж)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ярус";
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ярус");
	
КонецФункции

#КонецОбласти
