#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НачальноеЗаполнение(Команда)
	НачальноеЗаполнениеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьКаталогВременныхФайловОтОбработок(Команда)
	ОчиститьКаталогВременныхФайловОтОбработокНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОбработкиВКаталогеВременныхФайлов(Команда)
	ОбновитьОбработкиВКаталогеВременныхФайловНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НачальноеЗаполнениеНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	дкл_ТипыСообщений.Ссылка КАК Ссылка
		|ИЗ
		|	Перечисление.дкл_ТипыСообщений КАК дкл_ТипыСообщений";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

		МенеджерЗаписи = РегистрыСведений.дкл_Обработки.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ИмяОбработки = Строка(ВыборкаДетальныеЗаписи.Ссылка);
		МенеджерЗаписи.ВнутренняяОбработка = Истина;
		МенеджерЗаписи.Направление = Перечисления.дкл_НаправлениеСообщения.Исходящее;
		МенеджерЗаписи.ТипСообщения = ВыборкаДетальныеЗаписи.Ссылка;
		МенеджерЗаписи.Записать();
		
		МенеджерЗаписи = РегистрыСведений.дкл_Обработки.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ИмяОбработки = Строка(ВыборкаДетальныеЗаписи.Ссылка) + "In";
		МенеджерЗаписи.ВнутренняяОбработка = Истина;
		МенеджерЗаписи.Направление = Перечисления.дкл_НаправлениеСообщения.Входящее;
		МенеджерЗаписи.ТипСообщения = ВыборкаДетальныеЗаписи.Ссылка;
		МенеджерЗаписи.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьКаталогВременныхФайловОтОбработокНаСервере()
	КаталогОбработки = КаталогВременныхФайлов();
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	дкл_Обработки.ИмяОбработки КАК ИмяОбработки
		|ИЗ
		|	РегистрСведений.дкл_Обработки КАК дкл_Обработки";
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ИмяФайла = дкл_Обработчики.ПолучитьИмяФайлаОбработки(
			ВыборкаДетальныеЗаписи.ИмяОбработки, КаталогОбработки);
		УдалитьФайлы(ИмяФайла);
	КонецЦикла; 
КонецПроцедуры

&НаСервере
Процедура ОбновитьОбработкиВКаталогеВременныхФайловНаСервере()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	дкл_Обработки.ИмяОбработки КАК ИмяОбработки
		|ИЗ
		|	РегистрСведений.дкл_Обработки КАК дкл_Обработки";
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		РегистрыСведений.дкл_Обработки.ПодключитьОбработку(
			ВыборкаДетальныеЗаписи.ИмяОбработки, Перечисления.дкл_НаправлениеСообщения.Входящее);			
		РегистрыСведений.дкл_Обработки.ПодключитьОбработку(
			ВыборкаДетальныеЗаписи.ИмяОбработки, Перечисления.дкл_НаправлениеСообщения.Исходящее);			
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

