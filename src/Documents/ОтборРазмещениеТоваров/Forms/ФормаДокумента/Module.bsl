
&НаСервере
Процедура дкл_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Если ПолучитьФункциональнуюОпцию("дкл_ВключитьУправлениеПолкамиВДокументах") Тогда
		
		// ******   "Управление полками"    ***********
		
		НоваяКоманда = ЭтаФорма.Команды.Добавить("дкл_УправлениеПолками");
		НоваяКоманда.Действие = "дкл_УправлениеПолками";
		НоваяКоманда.Заголовок = "Управление полками";
		НоваяКоманда.ИзменяетСохраняемыеДанные = Ложь;
		НоваяКоманда.Отображение = ОтображениеКнопки.КартинкаИТекст;
		НоваяКоманда.Подсказка = "Управление полками АСХ Диком";	
		
		НовыйЭлемент = ЭтаФорма.Элементы.Вставить("ФормаДкл_УправлениеПолками",
		Тип("КнопкаФормы"),
		ЭтаФорма.Элементы.ТоварыОтбор.КоманднаяПанель);
		НовыйЭлемент.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;	
		НовыйЭлемент.Заголовок = "Управление полками";	 
		НовыйЭлемент.ИмяКоманды = "дкл_УправлениеПолками"; 

		// ******   "Управление полками"    ***********
		
		НоваяКоманда = ЭтаФорма.Команды.Добавить("дкл_УправлениеПолками_Размещение");
		НоваяКоманда.Действие = "дкл_УправлениеПолками_Размещение";
		НоваяКоманда.Заголовок = "Управление полками";
		НоваяКоманда.ИзменяетСохраняемыеДанные = Ложь;
		НоваяКоманда.Отображение = ОтображениеКнопки.КартинкаИТекст;
		НоваяКоманда.Подсказка = "Управление полками АСХ Диком";	
		
		НовыйЭлемент = ЭтаФорма.Элементы.Вставить("ФормаДкл_УправлениеПолками_Размещение",
		Тип("КнопкаФормы"),
		ЭтаФорма.Элементы.ТоварыРазмещение.КоманднаяПанель);
		НовыйЭлемент.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;	
		НовыйЭлемент.Заголовок = "Управление полками";	 
		НовыйЭлемент.ИмяКоманды = "дкл_УправлениеПолками_Размещение"; 
		
	КонецЕсли;
	
	// https://jira.dikomdev.ru/browse/ERPDEV-3531
	Если ПолучитьФункциональнуюОпцию("дкл_ИспользуетсяТСДСклад15ДляОтборов") Тогда
		
		// ******   Группа формы "дкл_Группа"    ***********
		
		Группа = Элементы.Вставить("дкл_Группа",Тип("ГруппаФормы"),Элементы.ШапкаПраво);
		Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		Группа.Заголовок = "Группа";
		Группа.Подсказка = "Группа";
		Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		Группа.ОтображатьЗаголовок = Ложь;
		Группа.Отображение = ОтображениеОбычнойГруппы.Нет;	
		
		// ******   Переместим поле формы "Статус"    ***********
		Элементы.Переместить(Элементы.Статус, Группа);
		
		// ******   Поля формы "дкл_Статус" и "дкл_Дата_Статуса"    ***********
		дкл_ИнтерфейсСервер.СоздатьРеквизит(ЭтаФорма, "дкл_Статус", 
		Новый ОписаниеТипов("ПеречислениеСсылка.дкл_СтатусыДокументов"));
		
		дкл_ИнтерфейсСервер.СоздатьРеквизит(ЭтаФорма, "дкл_ДатаСтатуса", 
		Новый ОписаниеТипов("Дата", , ,	Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
		
		Шапка_дкл_Статус = дкл_ИнтерфейсСервер.СздПолеНадписи(
		ЭтаФорма, "Шапка_дкл_Статус", Группа, "Склад 15", Ложь ,"дкл_Статус");
		
		Шапка_дкл_ДатаСтатуса = дкл_ИнтерфейсСервер.СздПолеНадписи(
		ЭтаФорма, "Шапка_дкл_ДатаСтатуса", Группа, "Дата", Ложь ,"дкл_ДатаСтатуса");
		
		Результат = РегистрыСведений.дкл_СтатусыДокументов.СтатусИДатаСтатусаДокумента(Объект.Ссылка);
		
		ЭтаФорма.дкл_Статус = Результат.Статус;
		ЭтаФорма.дкл_ДатаСтатуса = Результат.Дата;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура дкл_УправлениеПолками()
	ТоварыОтбор = дкл_УправлениеПолкамиНаСервере("ТоварыОтбор");      
	Если ТоварыОтбор.Количество() = 0 Тогда
		Текст = "Нет товаров к отбору.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(Текст);
		Возврат;
	КонецЕсли;
	ПараметрыФормы = Новый Структура("ТоварыОтбор", ТоварыОтбор);
	ОткрытьФорму("РегистрСведений.дкл_Полки.Форма.ФормаОтбора", 
		ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура дкл_УправлениеПолками_Размещение()
	ТоварыОтбор = дкл_УправлениеПолкамиНаСервере("ТоварыРазмещение");                     
	Если ТоварыОтбор.Количество() = 0 Тогда
		Текст = "Нет товаров к размещению.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(Текст);
		Возврат;
	КонецЕсли;
	ПараметрыФормы = Новый Структура("ТоварыОтбор", ТоварыОтбор);
	ОткрытьФорму("РегистрСведений.дкл_Полки.Форма.ФормаОтбора", 
		ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаСервере
Функция дкл_УправлениеПолкамиНаСервере(ТабЧасть)
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(Объект[ТабЧасть].Выгрузить(,
		"Ячейка,Артикул,Номенклатура,Характеристика,Упаковка,Серия,КоличествоУпаковок "));
КонецФункции

