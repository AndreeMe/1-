////////////////////////////////////////////////////////////////////////////////
// Общие методы форм, клиент
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// ЗаблокироватьКнопки
// 
// Параметры:
//  Форма - Форма - 
Процедура ЗаблокироватьКнопки(Форма) Экспорт

	Элементы = Форма.Элементы;
	Элементы.ФормаВыдачаПолки.Доступность = Ложь;		
	Элементы.ФормаВозвратПолки.Доступность = Ложь;		
	Элементы.ОбновитьСостояниеПолок.Доступность = Истина;
    Форма.БлокироватьКнопки = Истина;
	
КонецПроцедуры

// Разблокировать кнопки.
// 
// Параметры:
//  Форма - Форма - 
Процедура РазблокироватьКнопки(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Элементы.ФормаВыдачаПолки.Доступность = Истина;		
	Элементы.ФормаВозвратПолки.Доступность = Истина;
	Элементы.ОбновитьСостояниеПолок.Доступность = Истина;
	Форма.БлокироватьКнопки = Ложь;
	
КонецПроцедуры

// УстановитьДоступностьКнопокДляТекущейПолки
// 
// Параметры:
//  Форма - Форма - 
Процедура УстановитьДоступностьКнопокДляТекущейПолки(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;

	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;      

	// это все меняет (в карусели есть только одна кнопка)...
	Если Форма.ДоступнаТолькоВыдачаПолки Тогда
		Элементы.ФормаВыдачаПолки.Доступность  = Истина;
		Элементы.ФормаВозвратПолки.Доступность = Ложь;
		Возврат;
	КонецЕсли; 
	
	// если полка не выдана, то кнопка возврата не доступна...
	Если НЕ ТекущиеДанные.НомерСтатуса = 0 Тогда
		Элементы.ФормаВозвратПолки.Доступность = Истина;
	Иначе
		Элементы.ФормаВозвратПолки.Доступность = Ложь;
	КонецЕсли;      
	
	// для АСХ с одной зоной выдачи разрешаем выдавать не больше 1-й  полки,
	// с 2-мя - не больше 2-х полок...
	
	ВыданоПолок = Форма.Выдана.Количество();
	КоличествоЗонВыдачи = Форма.КоличествоЗонВыдачи;

	Если КоличествоЗонВыдачи = 1 
		И ВыданоПолок > 0  Тогда
		Элементы.ФормаВыдачаПолки.Доступность = Ложь;
	ИначеЕсли КоличествоЗонВыдачи = 2 
		И ВыданоПолок > 1 Тогда
		Элементы.ФормаВыдачаПолки.Доступность = Ложь;
	Иначе
		Элементы.ФормаВыдачаПолки.Доступность = Истина;
	КонецЕсли;
	
	// номер статуса лифта соответствует цвету индикатора:
	// НомерСтатуса = 1; // неполадки (красный)
	// НомерСтатуса = 3; // нет связи (серый)
	// НомерСтатуса = 4; // в работе (зеленый)
	// НомерСтатуса = 2; // выполняется операция (желтый)
	Если ТекущиеДанные.НомерСтатуса = 3 // полка в движении
			ИЛИ Форма.НомерСтатусаЛифта = 1
				ИЛИ Форма.НомерСтатусаЛифта = 3 // если лифт в движении, не блокируем кнопки...
					ИЛИ Форма.БлокироватьКнопки Тогда
		ЗаблокироватьКнопки(Форма);
	КонецЕсли;          
	
КонецПроцедуры

// УстановитьДоступностьКнопокДляТекущейПолкиВФормеОтбора
// 
// Параметры:
//  Форма - Форма - 
Процедура УстановитьДоступностьКнопокДляТекущейПолкиВФормеОтбора(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;

	// в форме списка нет полок, пока так...
	Попытка
		Если ТекущиеДанные = Неопределено И НЕ Форма.Полки.Количество() = 0 Тогда
			ТекущиеДанные = Форма.Полки[0];	
		КонецЕсли;      
	Исключение
	КонецПопытки;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;      
	
	// это все меняет (в карусели есть только одна кнопка)...
	Если Форма.ДоступнаТолькоВыдачаПолки Тогда               
		Если ТекущиеДанные.НомерСтатуса = 0 Тогда // полка свободна...
			Элементы.ФормаВыдачаПолки.Доступность  = Истина;
		Иначе
			Элементы.ФормаВыдачаПолки.Доступность  = Ложь;
		КонецЕсли;
		Элементы.ФормаВозвратПолки.Доступность = Ложь;
		Возврат;
	КонецЕсли; 
	
	// если полка не выдана, то кнопка возврата не доступна...
	Если НЕ ТекущиеДанные.НомерСтатуса = 0 Тогда
		Элементы.ФормаВозвратПолки.Доступность = Истина;
	Иначе
		Элементы.ФормаВозвратПолки.Доступность = Ложь;
	КонецЕсли;      

	НастройкаСклада = ТекущиеДанные.НастройкаСклада;
	
	// такое может быть в документе "Отбор и размещение товаров"...
	Если НЕ ЗначениеЗаполнено(НастройкаСклада) Тогда
		ЗаблокироватьКнопки(Форма);
		Возврат;
	КонецЕсли;
	
	НайденныеСтроки = Форма.Лифты.НайтиСтроки(Новый Структура("НастройкаСклада", НастройкаСклада));
	
	Если НЕ НайденныеСтроки.Количество() = 0 Тогда
		
		НайденнаяСтрока = НайденныеСтроки[0];	

		// для АСХ с одной зоной выдачи разрешаем выдавать не больше 1-й  полки,
		// с 2-мя - не больше 2-х полок...
		Если НайденнаяСтрока.КоличествоЗонВыдачи = 1 
				И НайденнаяСтрока.ВыданоПолок > 0  Тогда
			Элементы.ФормаВыдачаПолки.Доступность = Ложь;
		ИначеЕсли НайденнаяСтрока.КоличествоЗонВыдачи = 2 
				И НайденнаяСтрока.ВыданоПолок > 1 Тогда
			Элементы.ФормаВыдачаПолки.Доступность = Ложь;
		Иначе
			Элементы.ФормаВыдачаПолки.Доступность = Истина;
		КонецЕсли;
		
		Если НЕ НайденнаяСтрока.БуквенныйКод = "RDY" Тогда
			ЗаблокироватьКнопки(Форма);
		КонецЕсли;          
		
	КонецЕсли;
	
	Если Форма.БлокироватьКнопки Тогда
		ЗаблокироватьКнопки(Форма);
	КонецЕсли;          
	
КонецПроцедуры

Процедура ОбновитьСтатусЛифта(Форма) Экспорт

	Если НЕ ЗначениеЗаполнено(Форма.НастройкаСкладаЛифта) Тогда
		Форма.Элементы.Картинка.Картинка = БиблиотекаКартинок.ОформлениеКругПустой;
		Возврат;
	КонецЕсли;
	
	// в функции устанавливается значение НомерСтатусаЛифта
	дкл_ФормыВызовСервера.ПроверитьСоединение(Форма.НастройкаСкладаЛифта, Форма.НомерСтатусаЛифта);
	
	//НомерСтатуса = 3; // нет связи
	//НомерСтатуса = 4; // в работе
	//НомерСтатуса = 2; // выполняется операция
	//НомерСтатуса = 1; // ошибка
	
	Если Форма.НомерСтатусаЛифта = 4 Тогда
		Форма.Элементы.Картинка.Картинка = БиблиотекаКартинок.ОформлениеКругЗеленый;
	ИначеЕсли Форма.НомерСтатусаЛифта = 2 Тогда
		Форма.Элементы.Картинка.Картинка = БиблиотекаКартинок.ОформлениеКругЖелтый;
	ИначеЕсли Форма.НомерСтатусаЛифта = 1 Тогда
		Форма.Элементы.Картинка.Картинка = БиблиотекаКартинок.ОформлениеКругКрасный;
	Иначе
		Форма.Элементы.Картинка.Картинка = БиблиотекаКартинок.ОформлениеКругПустой;
	КонецЕсли;            
	
КонецПроцедуры  

#КонецОбласти
