#Область ПрограммныйИнтерфейс

//@skip-check export-procedure-missing-comment
//@skip-check doc-comment-parameter-section
//@skip-check doc-comment-export-function-return-section
Функция ОписаниеВебСервиса(ТипПодключаемойОбработки, Направление = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТипСообщения", ТипПодключаемойОбработки);
	
	Если НЕ БлокировкаРаботыСВнешнимиРесурсами.РаботаСВнешнимиРесурсамиЗаблокирована() Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	дкл_Обработки.НастройкаВебСервиса КАК НастройкаВебСервиса
		|ИЗ
		|	РегистрСведений.дкл_Обработки КАК дкл_Обработки
		|ГДЕ
		|	дкл_Обработки.ТипСообщения = &ТипСообщения";
		
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ
		|	дкл_Обработки.НастройкаВебСервисаДляКопии КАК НастройкаВебСервиса
		|ИЗ
		|	РегистрСведений.дкл_Обработки КАК дкл_Обработки
		|ГДЕ
		|	дкл_Обработки.ТипСообщения = &ТипСообщения";
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Текст = "Не задано описание веб-сервиса для сообщений вида " + ТипПодключаемойОбработки;
		ОбщегоНазначения.СообщитьПользователю(Текст);
		дкл_Логи.СообщитьОбОшибке(Текст);
		Возврат Неопределено;		
	КонецЕсли; 
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Если НЕ ЗначениеЗаполнено(Выборка.НастройкаВебСервиса) Тогда
		Текст = "Не задано описание веб-сервиса для копии для сообщений вида " + ТипПодключаемойОбработки;
		ОбщегоНазначения.СообщитьПользователю(Текст);
		дкл_Логи.СообщитьОбОшибке(Текст);
		Возврат Неопределено;		
	КонецЕсли; 
	
	Возврат Выборка.НастройкаВебСервиса;

КонецФункции // ()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СериализоватьОбъект(Значение) Экспорт
		
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьXML(ЗаписьXML, Значение);
	СтрокаXML = ЗаписьXML.Закрыть();	
	
	Возврат СтрокаXML;
	
КонецФункции 

Функция ДеСериализоватьОбъект(СтрокаXML) Экспорт
	
	ЧтениеXML = Новый ЧтениеXML();
	ЧтениеXML.УстановитьСтроку(СтрокаXML);
	
	Попытка
		Значение = СериализаторXDTO.ПрочитатьXML(ЧтениеXML);	
	Исключение
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки;
	
	Возврат Значение;
	
КонецФункции 

Функция ПолучитьНеСериализуемыеОбъекты(Объект, ВариантСериализации = "ХранилищеЗначения") Экспорт
	
	МассивПутей = Новый Массив();
	
	ДобавитьВМассивПутейНесериализуемыйОбъект(МассивПутей, Объект, ВариантСериализации);
	
	Возврат МассивПутей;
	
КонецФункции // ПолучитьНеСериализуемыеОбъекты()

 // Служебная рекурсия для определения путей несериализуемых объектов:

Процедура ДобавитьВМассивПутейНесериализуемыйОбъект(МассивПутей, Родитель, ВариантСериализации, ПутьКРодителю = "")
	
	Если ТипЗнч(Родитель) = Тип("Массив")
		ИЛИ ТипЗнч(Родитель) = Тип("ФиксированныйМассив") Тогда
		
		Счетчик = 0;
		Для каждого ЭлементРодителя Из Родитель Цикл
			ДобавитьВМассивПутейНесериализуемыйОбъект(
				МассивПутей, ЭлементРодителя, ВариантСериализации, ПутьКРодителю + "[" + Формат(Счетчик, "ЧН=0; ЧГ=") + "]");
			Счетчик = Счетчик + 1;
		КонецЦикла;
	ИначеЕсли ТипЗнч(Родитель) = Тип("Структура")
		ИЛИ ТипЗнч(Родитель) = Тип("Соответствие")
		ИЛИ ТипЗнч(Родитель) = Тип("ФиксированнаяСтруктура")
		ИЛИ ТипЗнч(Родитель) = Тип("ФиксированноеСоответствие")
		Тогда
		
		Для каждого ЭлементРодителя Из Родитель Цикл
			
			ДобавитьВМассивПутейНесериализуемыйОбъект(МассивПутей, ЭлементРодителя.Ключ,     ВариантСериализации, ПутьКРодителю + "/Ключ");
			ДобавитьВМассивПутейНесериализуемыйОбъект(МассивПутей, ЭлементРодителя.Значение, ВариантСериализации, ПутьКРодителю + "/" + ЭлементРодителя.Ключ);
			
		КонецЦикла;
		
	Иначе
		Попытка
			
			Если ВариантСериализации = "ХранилищеЗначения" Тогда
				// Результат = Новый ХранилищеЗначения(Родитель);
			ИначеЕсли ВариантСериализации = "ВременноеХранилище" Тогда
				Адрес = ПоместитьВоВременноеХранилище(Родитель);
				УдалитьИзВременногоХранилища(Адрес);
			КонецЕсли;
			
		Исключение
			МассивПутей.Добавить(ПутьКРодителю);
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры // ДобавитьВМассивПутейНесериализуемыйОбъект()

#КонецОбласти

